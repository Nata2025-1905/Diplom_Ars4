	
#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПланировщикНаСервере();
		
КонецПроцедуры 

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Документ Обслуживание клинтов записан" Тогда
		
		Планировщик.Элементы.Очистить();
		ЗаполнитьПланировщикНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланировщикПередНачаломРедактирования(Элемент, НовыйЭлемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Ключ", Элемент.ВыделенныеЭлементы[0].Значение); 
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиентов.ФормаОбъекта", ПараметрыФормы);
				
КонецПроцедуры 

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Парам = Новый Структура();
	Парам.Вставить("ВремяНачалаРабот", Начало);
	Парам.Вставить("ВремяОкончанияРабот", Конец);
	Парам.Вставить("Дата", Дата(Начало));
	
	Специалист = ЗначенияИзмерений.Получить("Сотрудники");
	Парам.Вставить("Специалист", Специалист); 
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиентов.ФормаОбъекта", Парам);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПланировщикНаСервере()

	ИзмеренияПланировщика = Планировщик.Измерения;
	ИзмеренияПланировщика.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_Сотрудники.Ссылка КАК Ссылка,
	|	ВКМ_Сотрудники.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ВКМ_Сотрудники КАК ВКМ_Сотрудники
	|ГДЕ
	|	ВКМ_Сотрудники.Категория = ЗНАЧЕНИЕ(Перечисление.ВКМ_КатегорияСотрудника.Специалист)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСотрудники = РезультатЗапроса.Выбрать();
	
	ИзмеренияСотрудники = ИзмеренияПланировщика.Добавить("Сотрудники");
	
	Пока ВыборкаСотрудники.Следующий() Цикл
		
		НовыйСотрудник = ИзмеренияСотрудники.Элементы.Добавить(ВыборкаСотрудники.Ссылка);
		НовыйСотрудник.Текст = ВыборкаСотрудники.Наименование;
		
	КонецЦикла;
	
	ЭлементыПланировщика = Планировщик.Элементы;
	ЭлементыПланировщика.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиентов.Ссылка КАК Ссылка,
	|	ВКМ_ОбслуживаниеКлиентов.Специалист КАК Специалист,
	|	ВКМ_ОбслуживаниеКлиентов.ВремяНачалаРабот КАК ВремяНачалаРабот,
	|	ВКМ_ОбслуживаниеКлиентов.ВремяОкончанияРабот КАК ВремяОкончанияРабот,
	|	ВКМ_ОбслуживаниеКлиентов.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
	|	ВКМ_ОбслуживаниеКлиентов.Клиент КАК Клиент,
	|	ВКМ_ОбслуживаниеКлиентов.ОписаниеПроблемы КАК ОписаниеПроблемы
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиентов КАК ВКМ_ОбслуживаниеКлиентов";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СоответствиеЗначений = Новый Соответствие;
		СоответствиеЗначений.Вставить("Сотрудники", Выборка.Специалист);
		
		ВремяНачало = Час(Выборка.ВремяНачалаРабот)*60*60;
		ВремяОкончания = Час(Выборка.ВремяОкончанияРабот)*60*60;
		
		ДатаНачала = НачалоДня(Выборка.ДатаПроведенияРабот) + ВремяНачало;
		ДатаОкончание  = НачалоДня(Выборка.ДатаПроведенияРабот) + ВремяОкончания; 
		
		НовыйЭлемент = Планировщик.Элементы.Добавить(ДатаНачала, ДатаОкончание);
		
		НовыйЭлемент.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
		
		ТекстовоеПоле = Выборка.Клиент;
		ТекстовоеПоле1 = Выборка.ОписаниеПроблемы;
		
		НовыйЭлемент.Текст = "Клиент: " + Строка(ТекстовоеПоле) + Символы.ПС + "Описание проблемы: " + Строка(ТекстовоеПоле1);
		
		НовыйЭлемент.Значение = Выборка.Ссылка;
		НовыйЭлемент.ЦветФона = WebЦвета.ЗеленаяЛужайка;
		НовыйЭлемент.ЦветРамки = WebЦвета.ТемноСерый;
		
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти



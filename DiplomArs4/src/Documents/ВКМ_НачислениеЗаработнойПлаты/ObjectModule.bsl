
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОсновныеНачисленияИУдержания.Количество() = 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет данных в документе";
		Сообщение.Сообщить();
		
		Отказ = Истина;		
	КонецЕсли;
	
	СформироватьДвиженияОсновныеНачисления();
	СформироватьДвиженияУдержания();
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать();
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
	РассчитатьОклад();
	РассчитатьОтпуск();
	РассчитатьНДФЛ();
	
	СформироватьДвижениеВзаиморасчетаССотрудниками();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияОсновныеНачисления()
	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	
	Для Каждого Строка Из ОсновныеНачисленияИУдержания Цикл 
		
		Если Строка.Начисление = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.ГрафикРаботы = Строка.ГрафикРаботы;
			Движение.ВидРасчета = Строка.Начисление;
			Движение.Показатель = Строка.СуммаНачисления;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			
		КонецЕсли;
		
		Если Строка.Начисление = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.ГрафикРаботы = Строка.ГрафикРаботы;
			Движение.ВидРасчета = Строка.Начисление;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			
			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Строка.ДатаОкончания, - 1));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияУдержания()
	
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	Для Каждого Строка Из ОсновныеНачисленияИУдержания Цикл 
		
		Если Строка.Удержание = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ Тогда
			
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.ВидРасчета = Строка.Удержание;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания; 
			Движение.БазовыйПериодНачало = Строка.ДатаНачала;
			Движение.БазовыйПериодКонец = Строка.ДатаОкончания;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК План,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Ссылка
	|				И ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Оклад)) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
		Движение.ОтработаноДней = Выборка.Факт;
		Движение.Результат = Движение.Показатель / Выборка.План * Выборка.Факт;
		
	КонецЦикла; 
	
	Движения.ВКМ_ОсновныеНачисления.Записать( ,Истина);	
	
КонецПроцедуры 

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза КАК РезультатБазаОклад,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОтработаноДнейБаза, 0) КАК БазаРабочиеДни,
	|	ЕСТЬNULL(РАЗНОСТЬДАТ(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияНачало, ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ПериодДействияКонец, ДЕНЬ) + 1, 0) КАК ОтпускКалендарныеДни,
	|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК РезультатБазаПремия
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	|			&Измерения,
	|			&Измерения,
	|			,
	|			Регистратор = &Ссылка
	|				И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения, , ) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
	|		ПО ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Сотрудник = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.Сотрудник";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
		Движение.ОтработаноДней = Выборка.ОтпускКалендарныеДни;
		Движение.Результат = (Выборка.РезультатБазаОклад + Выборка.РезультатБазаПремия) / Выборка.БазаРабочиеДни * Выборка.ОтпускКалендарныеДни;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать( ,Истина);	
	
КонецПроцедуры

Процедура РассчитатьНДФЛ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки) КАК НомерСтроки,
	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза КАК РезультатБаза,
	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтрокиРазрез КАК НомерСтрокиРазрез
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(
	|			&Измерения,
	|			&Измерения,
	|			&НомерСтроки,
	|			Регистратор = &Ссылка
	|				И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтрокиРазрез,
	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РезультатБаза";
	
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	НомерСтроки = Новый Массив;
	НомерСтроки.Добавить("НомерСтроки");
	Запрос.УстановитьПараметр("НомерСтроки", НомерСтроки );
	
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ПроцентНДФЛ = Константы.ВКМ_НДФЛ.Получить();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_Удержания[Выборка.НомерСтроки -1];
		Движение.Показатель = Выборка.РезультатБаза;
		Движение.Результат = Выборка.РезультатБаза / 100 * ПроцентНДФЛ;
		
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать( ,Истина);	
	
КонецПроцедуры

Процедура СформироватьДвижениеВзаиморасчетаССотрудниками()
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_ОсновныеНачисления.Результат) КАК Результат,
	|	МИНИМУМ(ВКМ_ОсновныеНачисления.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ОсновныеНачисления
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_Удержания.Результат) КАК Результат,
	|	МИНИМУМ(ВКМ_Удержания.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_Удержания
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_Удержания.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ВТ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВТ_ОсновныеНачисления.Результат, 0) КАК СуммаОсновныеНачисления,
	|	ЕСТЬNULL(ВТ_Удержания.Результат, 0) КАК СуммаУдержания
	|ИЗ
	|	ВТ_ОсновныеНачисления КАК ВТ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Удержания КАК ВТ_Удержания
	|		ПО ВТ_ОсновныеНачисления.Сотрудник = ВТ_Удержания.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = (Дата);
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.СуммаОсновныеНачисления - Выборка.СуммаУдержания;	
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти




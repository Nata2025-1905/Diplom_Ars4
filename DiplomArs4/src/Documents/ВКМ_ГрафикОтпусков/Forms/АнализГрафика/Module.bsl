
#Область ОбработчикиСобытийФормы

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаРасчета = Параметры.Дата;
	ГодРасчета = Параметры.Год;
	
	Дата = Дата(ГодРасчета,Месяц(ДатаРасчета), День(ДатаРасчета));
	Год = ГодРасчета;
	
КонецПроцедуры 
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Диаграмма.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусковОбороты.Сотрудник КАК Сотрудник,
		|	ВКМ_ГрафикОтпусковОбороты.ДатаНачала КАК ДатаНачала,
		|	ВКМ_ГрафикОтпусковОбороты.ДатаОкончания КАК ДатаОкончания
		|ИЗ
		|	РегистрНакопления.ВКМ_ГрафикОтпусков.Обороты(&НачалоПериод, &КонецПериод, , Год = &Год) КАК ВКМ_ГрафикОтпусковОбороты";
	
	
	Запрос.УстановитьПараметр("КонецПериод", КонецГода(Дата));
	Запрос.УстановитьПараметр("НачалоПериод", НачалоГода(Дата));
	Запрос.УстановитьПараметр("Год", Год);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Серия = Диаграмма.УстановитьСерию("Отпуск");
	Серия.Текст = "Отпуск";
	Серия.Цвет = WebЦвета.Аквамарин;
	
	Пока Выборка.Следующий() Цикл
		
		 Точка = Диаграмма.УстановитьТочку(Выборка.Сотрудник);
		 Точка.Текст = Выборка.Сотрудник;
		 Значение = Диаграмма.ПолучитьЗначение(Точка, Серия);
		 
		 Интервал = Значение.Добавить();
		 Интервал.Начало = Выборка.ДатаНачала;
		 Интервал.Конец = Выборка.ДатаОкончания;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти





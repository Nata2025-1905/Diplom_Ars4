
#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)
	
	ИзменяемаяСтрока = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	
	Если Год(ИзменяемаяСтрока.ДатаНачала) <> Объект.Год Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В строке №: " + ИзменяемаяСтрока.НомерСтроки + " выбранная дата, не соответствует указанному году! Поменяйте дату!";
		Сообщение.Поле = "Объект.ОтпускаСотрудников[" + (ИзменяемаяСтрока.НомерСтроки -1) + "].ДатаНачала";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)
	
		ИзменяемаяСтрока = Элементы.ОтпускаСотрудников.ТекущиеДанные;
		
	Если Год(ИзменяемаяСтрока.ДатаОкончания) <> Объект.Год Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В строке №: " + ИзменяемаяСтрока.НомерСтроки + " выбранная дата, не соответствует указанному году. Поменяйте дату!";
		Сообщение.Поле = "Объект.ОтпускаСотрудников[" + (ИзменяемаяСтрока.НомерСтроки -1) + "].ДатаОкончания";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПревышениеОтпуска(Команда)
	
	ПроверитьПревышениеОтпускаНаСервере();
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьГрафик(Команда) 
	
	ПараметрыВвода = Новый Структура;
	ПараметрыВвода.Вставить("Дата", Объект.Дата);
	ПараметрыВвода.Вставить("Год", Объект.Год);
	
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыВвода, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьПревышениеОтпускаНаСервере()
		
	УсловноеОформление.Элементы.Очистить();
	
	ТабЗначений = Объект.ОтпускаСотрудников.Выгрузить();
	ТабЗначений.Колонки.Добавить("Длительность", Новый ОписаниеТипов("Число"));
	
	Для Каждого СтрокаТЗ Из ТабЗначений Цикл 		
		СтрокаТЗ.Длительность = (НачалоДня(СтрокаТЗ.ДатаОкончания) - НачалоДня(СтрокаТЗ.ДатаНачала)) / 86400 +1;	
	КонецЦикла;  
	
	ТабЗначений.Свернуть("Сотрудник", "Длительность");
	
	Для Каждого СтрокаТЗ Из ТабЗначений Цикл 
		
		Если СтрокаТЗ.Длительность > 28 Тогда
			
			ЭлементОформленияЛосось = УсловноеОформление.Элементы.Добавить();
			ЭлементОформленияЛосось.Использование = Истина;
			ЭлементОформленияЛосось.Оформление.УстановитьЗначениеПараметра("ЦветФона",WebЦвета.ЛососьСветлый);
			
			ЭлементУсловияЛосось = ЭлементОформленияЛосось.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементУсловияЛосось.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтпускаСотрудников.Сотрудник");
			ЭлементУсловияЛосось.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементУсловияЛосось.ПравоеЗначение =  СтрокаТЗ.Сотрудник;
			ЭлементУсловияЛосось.Использование = Истина;
			
			ОформляемоеПоле = ЭлементОформленияЛосось.Поля.Элементы.Добавить();
			ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОтпускаСотрудников");
			
		КонецЕсли;	
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти





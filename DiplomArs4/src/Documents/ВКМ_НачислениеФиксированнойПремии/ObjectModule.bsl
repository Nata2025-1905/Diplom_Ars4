
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ПремияСотрудникам.Количество() = 0 Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нет данных в документе";
		Сообщение.Сообщить();
		
		Отказ = Истина;		
	КонецЕсли;
	
	СформироватьДвиженияДополнительныеНачисления();
	СформироватьДвиженияУдержания();
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать(); 
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
	СформироватьДвиженияВзаиморасчетаССотрудниками();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьАвтозаполнение() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник КАК Сотрудник,
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот КАК СуммаПремии
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты";
	
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Количество() <> 0 Тогда
			
			НоваяСтрокаТЧ = ПремияСотрудникам.Добавить();
			НоваяСтрокаТЧ.Сотрудник = Выборка.Сотрудник;
			НоваяСтрокаТЧ.СуммаПремии = Выборка.СуммаПремии;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияДополнительныеНачисления()
	
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	
	Для Каждого Строка Из ПремияСотрудникам Цикл
		
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Результат = Строка.СуммаПремии;
		
	КонецЦикла;
	
КонецПроцедуры 

Процедура СформироватьДвиженияУдержания()
	
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	ПроцентНДФЛ = Константы.ВКМ_НДФЛ.Получить();
	
	Для Каждого Строка Из ПремияСотрудникам Цикл
		
		Движение = Движения.ВКМ_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Показатель = Строка.СуммаПремии;
		Движение.Результат = Движение.Показатель / 100 * ПроцентНДФЛ;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияВзаиморасчетаССотрудниками()
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВКМ_ДополнительныеНачисления.НомерСтроки) КАК НомерСтроки,
	|	ВКМ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_ДополнительныеНачисления.Результат) КАК Результат
	|ПОМЕСТИТЬ ВТ_ДополнительныеНачисления
	|ИЗ
	|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
	|ГДЕ
	|	ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ДополнительныеНачисления.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ВКМ_Удержания.НомерСтроки) КАК НомерСтроки,
	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	|	СУММА(ВКМ_Удержания.Результат) КАК Результат
	|ПОМЕСТИТЬ ВТ_Удержания
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_Удержания.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
	|	ВТ_ДополнительныеНачисления.Результат КАК СуммаДополнительныеНачисления,
	|	ВТ_Удержания.Результат КАК СуммаУдержания
	|ИЗ
	|	ВТ_ДополнительныеНачисления КАК ВТ_ДополнительныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Удержания КАК ВТ_Удержания
	|		ПО ВТ_ДополнительныеНачисления.Сотрудник = ВТ_Удержания.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.СуммаДополнительныеНачисления - Выборка.СуммаУдержания;	
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
